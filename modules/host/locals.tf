locals {
  ssh_public_key             = trimspace(var.ssh_public_key)
  ssh_additional_public_keys = [for key in var.ssh_additional_public_keys : trimspace(key) if trimspace(key) != ""]
  ssh_authorized_keys        = concat([local.ssh_public_key], local.ssh_additional_public_keys)

  # ssh_agent_identity is not set if the private key is passed directly, but if ssh agent is used, the public key tells ssh agent which private key to use.
  # For terraforms provisioner.connection.agent_identity, we need the public key as a string.
  ssh_agent_identity = var.ssh_private_key == null ? local.ssh_public_key : null

  # Keep var.name as the random_string keeper and allow an existing host to be
  # renamed without rotating its suffix or changing its Terraform address.
  base_name = trimspace(var.name_override) != "" ? trimspace(var.name_override) : var.name
  name      = var.append_random_suffix ? "${local.base_name}-${random_string.server.id}" : local.base_name

  # check if the user has set dns servers
  has_dns_servers = length(var.dns_servers) > 0

  effective_firewall_ids = var.firewall_ids == null ? toset(var.extra_firewall_ids) : setunion(var.firewall_ids, toset(var.extra_firewall_ids))
  extra_network_ids = toset([
    for network_id in var.extra_network_ids : network_id
    if network_id != var.primary_network_key
  ])

  default_connection_host = coalesce(
    hcloud_server.server.ipv4_address,
    hcloud_server.server.ipv6_address,
    try(
      [for network in hcloud_server.server.network : network.ip if var.network_id != null && network.network_id == var.network_id][0],
      try([for network in hcloud_server.server.network : network.ip][0], null)
    )
  )

  map_connection_host = (
    trimspace(lookup(var.node_connection_overrides, local.name, "")) != ""
    ? trimspace(lookup(var.node_connection_overrides, local.name, ""))
    : (
      trimspace(lookup(var.node_connection_overrides, var.name, "")) != ""
      ? trimspace(lookup(var.node_connection_overrides, var.name, ""))
      : null
    )
  )
  suffix_connection_host = trimspace(var.connection_host_suffix) != "" ? "${local.name}.${trim(trimspace(var.connection_host_suffix), ".")}" : null

  provisioner_connection_host = coalesce(
    trimspace(var.connection_host) != "" ? trimspace(var.connection_host) : null,
    local.map_connection_host,
    local.suffix_connection_host,
    local.default_connection_host
  )
}
